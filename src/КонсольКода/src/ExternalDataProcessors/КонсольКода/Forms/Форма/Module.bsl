

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//EDT 1.8.0.808 не добавляет на форму реквизит дерева значений с типом "ТаблицаЗначений"
	ТаблицаПараметров = Элементы.Добавить("ТаблицаПараметров", Тип("ТаблицаФормы"), Элементы.ТекущийКод);
	ТаблицаПараметров.ПутьКДанным = "Элементы.ДеревоКода.ТекущиеДанные.ПараметрыКода";
	
	ПолеТаблицы = Элементы.Добавить("ПараметрКода", Тип("ПолеФормы"), ТаблицаПараметров);
	ПолеТаблицы.ПутьКДанным ="Элементы.ДеревоКода.ТекущиеДанные.ПараметрыКода.Параметр";
	ПолеТаблицы.Вид = ВидПоляФормы.ПолеВвода; 
	
	ПолеТаблицы = Элементы.Добавить("ЗначениеПараметраКода", Тип("ПолеФормы"), ТаблицаПараметров);
	ПолеТаблицы.ПутьКДанным ="Элементы.ДеревоКода.ТекущиеДанные.ПараметрыКода.Значение";
	ПолеТаблицы.Вид = ВидПоляФормы.ПолеВвода; 
		
КонецПроцедуры


&НаКлиенте
Процедура ВыполнитьКодНаКлиенте(Команда)

	ПараметрыКода = ПодготовитьКод(Элементы.ДеревоКода.ТекущиеДанные);
	Если НЕ ПараметрыКода = Неопределено Тогда
		Выполнить(ПараметрыКода.Код);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКодНаСервере(Команда)

	ПараметрыКода = ПодготовитьКод(Элементы.ДеревоКода.ТекущиеДанные);
	Если НЕ ПараметрыКода = Неопределено Тогда
		ВыполнитьКодНаСервереВызовСервера(ПараметрыКода);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыполнитьКодНаСервереВызовСервера(ПараметрыКода)

	Выполнить(ПараметрыКода.Код);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПодготовитьКод(ДанныеВыполнения)
	
	Если ПустаяСтрока(ДанныеВыполнения.Код) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЗначенияПараметров = Новый Массив;	
	ИнициализацияПараметров = "";

	Для Каждого ДанныеПараметра Из ДанныеВыполнения.ПараметрыКода Цикл
		ЗначенияПараметров.Добавить(ДанныеПараметра.Значение);	
		ИнициализацияПараметров = ИнициализацияПараметров + ДанныеПараметра.Параметр + 
		" = ПараметрыКода.ЗначенияПараметров[" + ЗначенияПараметров.ВГраница() + "];";	
	КонецЦикла;
	
	ПараметрыКода = Новый Структура("Код, ЗначенияПараметров", 
	ИнициализацияПараметров + ДанныеВыполнения.Код, ЗначенияПараметров);					

	Возврат ПараметрыКода;
	
КонецФункции

&НаКлиенте
Процедура ДеревоСохранить(Команда)

	ПоказатьДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);

КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗагрузить(Команда)

	ПоказатьДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);

КонецПроцедуры

&НаКлиенте
Процедура ВыборФайлаКодаЗавершение(ВыбранныеФайлы, РежимДиалога) Экспорт

	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИмяФайла = ВыбранныеФайлы[0];
	ФайлКонсоли = Новый ТекстовыйДокумент();	

	Если РежимДиалога = РежимДиалогаВыбораФайла.Сохранение Тогда
		ФайлКонсоли.УстановитьТекст(ПолучитьДанныеДерева());
		ФайлКонсоли.Записать(ИмяФайла);
	ИначеЕсли РежимДиалога = РежимДиалогаВыбораФайла.Открытие Тогда
		ФайлКонсоли.Прочитать(ИмяФайла);
		УстановитьДанныеДерева(ФайлКонсоли.ПолучитьТекст());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеДерева()

	ДанныеДерева = Новый ХранилищеЗначения(РеквизитФормыВЗначение("ДеревоКода"));
	Возврат XMLСтрока(ДанныеДерева);

КонецФункции

&НаСервере
Процедура УстановитьДанныеДерева(ДанныеФайла)
	
	ДанныеДерева = XMLЗначение(Тип("ХранилищеЗначения"), ДанныеФайла).Получить();
	ЗначениеВРеквизитФормы(ДанныеДерева, "ДеревоКода"); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДиалогВыбораФайла(РежимДиалога) Экспорт
	
	ОбработчикВыбора = Новый ОписаниеОповещения("ВыборФайлаКодаЗавершение", ЭтотОбъект, РежимДиалога);
	ДиалогСохранения = Новый ДиалогВыбораФайла(РежимДиалога);
	ДиалогСохранения.Фильтр = "Код 1С | *.code1с";
	ДиалогСохранения.Показать(ОбработчикВыбора); 	
	
КонецПроцедуры